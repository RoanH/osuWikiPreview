plugins{
	id 'java-library'
	id 'maven-publish'
	id 'eclipse'
	id 'application'
	id 'com.github.johnrengelman.shadow' version '7.1.2'
	id 'com.github.spotbugs' version '5.0.14'
}

sourceSets{
	main{
		java{
			srcDirs = ['src']
		}
		resources{
			srcDirs = ['src']
		}
	}
}

spotbugs{
	showProgress = true
	effort = 'max'
	reportLevel = 'low'
	excludeFilter = file("exclude.xml")
}

spotbugsMain{
	reports{
		html{
			required = true
			outputLocation = file("$buildDir/reports/spotbugs/main/spotbugs.html")
			stylesheet = 'fancy-hist.xsl'
		}
	}
}

repositories{
	maven{
		allowInsecureProtocol = true
		url "$nexusPublic"
	}
}

dependencies{
	//Isla Lib
	implementation "dev.roanh.islalib:islalib:7.14"
	
	//JGit
	implementation 'org.eclipse.jgit:org.eclipse.jgit:6.5.0.202303070854-r'
	implementation 'org.eclipse.jgit:org.eclipse.jgit.ssh.apache:6.5.0.202303070854-r'
}

eclipse.classpath.downloadJavadoc = true
compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.17

javadoc{
	options.memberLevel = JavadocMemberLevel.PRIVATE
}

application{
	mainClassName = 'dev.roanh.wiki.Main'
}

group = 'dev.roanh.wiki'
if(project.hasProperty("releaseVersion")){
	version = releaseVersion
}else{
	version = '1.x-SNAPSHOT'
}

publishing{
	publications{
		nexus(MavenPublication){
			from components.java
			artifactId = 'wiki'
		
			pom{
				name = project.eclipse.project.name
				description = 'osu! wiki preview site'
				url = 'https://git.roanh.dev/roan/osuwikipreview'
				licenses{
					license{
						name = 'GNU Affero General Public License v3.0'
						url = 'https://www.gnu.org/licenses/agpl-3.0.txt'
					}
				}
				developers{
					developer{
						id = 'RoanH'
						name = 'Roan Hofland'
						email = 'roan@roanh.dev'
					}
				}
				scm{
					connection = 'scm:git:git@git.roanh.dev:roan/osuwikipreview.git'
					developerConnection = 'scm:git:git@git.roanh.dev:roan/osuwikipreview.git'
					url = 'https://git.roanh.dev/roan/osuwikipreview'
				}
			}
		}
	}

	if(project.hasProperty("mavenPassword")){	
		repositories{
			maven{
				url version.endsWith('SNAPSHOT') ? "$nexusSnapshots" : "$nexusReleases"
				credentials{
					username "$mavenUser"
					password "$mavenPassword"
				}
			}
		}
	}
}

shadowJar{
	into("META-INF/maven/$project.group/$project.publishing.publications.nexus.artifactId"){
		from{
			generatePomFileForNexusPublication
		}
		rename ".*", "pom.xml"
	}
}