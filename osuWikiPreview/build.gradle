plugins{
	id 'eclipse'
	id 'java'
	id 'application'
	id 'maven-publish'
	id 'com.github.johnrengelman.shadow' version '7.1.2'
	id 'com.github.spotbugs' version '5.0.14'
	id 'org.sonarqube' version '4.4.1.3373'
}

sourceSets{
	main.java.srcDirs = ['src']
}

dependencies{
	//Isla Lib
	implementation "dev.roanh.islalib:islalib:7.24"
	
	//JGit
	implementation 'org.eclipse.jgit:org.eclipse.jgit:6.5.0.202303070854-r'
	implementation 'org.eclipse.jgit:org.eclipse.jgit.ssh.apache:6.5.0.202303070854-r'
}

repositories{
	maven{
		allowInsecureProtocol = true
		url "$nexusPublic"
	}
}

version = findProperty("refName") ?: 'SNAPSHOT'
if(version.matches("v\\d+\\.\\d+")){
	version = version.substring(1)
}

eclipse.classpath.downloadSources = true
eclipse.classpath.downloadJavadoc = true
compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.17
javadoc.options.memberLevel = JavadocMemberLevel.PRIVATE
group = 'dev.roanh.wiki'
ext.artifact = 'wiki'
shadowJar.archiveName = 'osuWikiPreview-v' + version + '.jar'
application.mainClassName = 'dev.roanh.wiki.Main'

sonar{
	properties{
		property "sonar.projectKey", "roan_osuwikipreview_d8e2a73a-0db4-47c2-8b68-b3b9a5199f7b"
		property "sonar.projectName", "osuWikiPreview"
	}
}

spotbugs{
	showProgress = true
	effort = 'max'
	reportLevel = 'low'
	excludeFilter = file("$rootDir/exclude.xml")
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask){
	reports{
		html{
			required = true
			outputLocation = file("$buildDir/reports/spotbugs/main/spotbugs.html")
			stylesheet = 'fancy-hist.xsl'
		}
	}
}

publishing{
	publications{
		mavenJava(MavenPublication){
			from components.java
			artifactId = project.artifact

			pom{
				name = project.eclipse.project.name
				description = 'osu! wiki preview site'
				url = 'https://git.roanh.dev/roan/osuwikipreview'
				licenses{
					license{
						name = 'GNU Affero General Public License v3.0'
						url = 'https://www.gnu.org/licenses/agpl-3.0.txt'
					}
				}
				developers{
					developer{
						id = 'RoanH'
						name = 'Roan Hofland'
						email = 'roan@roanh.dev'
					}
				}
				scm{
					connection = 'scm:git:git@git.roanh.dev:roan/osuwikipreview.git'
					developerConnection = 'scm:git:git@git.roanh.dev:roan/osuwikipreview.git'
					url = 'https://git.roanh.dev/roan/osuwikipreview'
				}
			}
		}
	}
}

shadowJar{
	mergeServiceFiles()
	into("META-INF/maven/$project.group/$project.artifact"){
		from{
			generatePomFileForMavenJavaPublication
		}
		rename ".*", "pom.xml"
	}
}
