plugins{
	id 'eclipse'
	id 'application'
	id 'maven-publish'
	id 'com.gradleup.shadow' version '8.3.2'
	id 'com.github.spotbugs' version '6.0.18'
	id 'jacoco'
	id 'org.sonarqube' version '5.1.0.4882'
}

import com.github.spotbugs.snom.Confidence
import com.github.spotbugs.snom.Effort

sourceSets{
	main.java.srcDirs = ['src']

	test{
		java.srcDirs = ['test']
		resources.srcDirs = ['test']
	}
}

dependencies{
	//Isla Lib
	implementation "dev.roanh.islalib:islalib:7.31"
	
	//JGit
	implementation 'org.eclipse.jgit:org.eclipse.jgit:6.5.0.202303070854-r'
	implementation 'org.eclipse.jgit:org.eclipse.jgit.ssh.apache:6.5.0.202303070854-r'

	//Testing
	testImplementation(platform('org.junit:junit-bom:5.11.1'))
	testImplementation('org.junit.jupiter:junit-jupiter')
	testImplementation('org.wiremock:wiremock:3.5.4')
}

repositories{
	//Local cache
	if(project.hasProperty("nexusPublic")){
		maven{
			allowInsecureProtocol = true
			url "$nexusPublic"
		}
	}
	mavenCentral()
}

version = findProperty("refName") ?: 'SNAPSHOT'
if(version.matches("v\\d+\\.\\d+")){
	version = version.substring(1)
}

eclipse.classpath.downloadSources = true
eclipse.classpath.downloadJavadoc = true
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
jacocoTestReport.reports.xml.required = true
sourceCompatibility = 1.17
javadoc.options.memberLevel = JavadocMemberLevel.PRIVATE
javadoc.options.encoding = 'UTF-8'
group = 'dev.roanh.wiki'
ext.artifact = 'wiki'
shadowJar.archiveBaseName = 'osuWikiPreview'
shadowJar.archiveVersion = 'v' + version
shadowJar.archiveClassifier = ''
application.mainClass = 'dev.roanh.wiki.Main'

test{
	useJUnitPlatform()
	dependsOn 'cleanTest'

	testLogging{
		events "passed", "skipped", "failed"
		showStandardStreams = true
	}
}

sonar{
	properties{
		property "sonar.projectKey", "roan_osuwikipreview_d8e2a73a-0db4-47c2-8b68-b3b9a5199f7b"
		property "sonar.projectName", "osuWikiPreview"
	}
}

spotbugs{
	showProgress = true
	effort = Effort.valueOf('MAX')
	reportLevel = Confidence.valueOf('LOW')
	excludeFilter = file("$rootDir/exclude.xml")
}

tasks.withType(com.github.spotbugs.snom.SpotBugsTask){
	reports{
		html{
			required = true
			outputLocation = file("$buildDir/reports/spotbugs/main/spotbugs.html")
			stylesheet = 'fancy-hist.xsl'
		}
	}
}

publishing{
	publications{
		mavenJava(MavenPublication){
			from components.java
			artifactId = project.artifact

			pom{
				name = project.eclipse.project.name
				description = 'osu! wiki preview site'
				url = 'https://github.com/RoanH/osuWikiPreview'
				licenses{
					license{
						name = 'GNU Affero General Public License v3.0'
						url = 'https://www.gnu.org/licenses/agpl-3.0.txt'
					}
				}
				developers{
					developer{
						id = 'RoanH'
						name = 'Roan Hofland'
						email = 'roan@roanh.dev'
					}
				}
				scm{
					connection = 'scm:git:git@github.com:RoanH/osuWikiPreview.git'
					developerConnection = 'scm:git:git@git.roanh.dev:roan/osuwikipreview.git'
					url = 'https://github.com/RoanH/osuWikiPreview'
				}
			}
		}
	}
}

shadowJar{
	mergeServiceFiles()
	into("META-INF/maven/$project.group/$project.artifact"){
		from{
			generatePomFileForMavenJavaPublication
		}
		rename ".*", "pom.xml"
	}
}
